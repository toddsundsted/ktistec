# Outbox
rule "outbox"
  condition Outgoing, actor, activity
  none Outbox, owner: actor, activity: activity
  assert Outbox, owner: actor, activity: activity
  assert activity, IsAddressedTo, actor
end

# Inbox

rule "inbox"
  condition Incoming, actor, activity
  condition Actor, actor, iri: iri
  condition iri, IsRecipient
  none Inbox, owner: actor, activity: activity
  assert Inbox, owner: actor, activity: activity
  assert activity, IsAddressedTo, actor
end

rule "inbox"
  condition Incoming, actor, activity
  condition Activity, activity, actor: sender
  condition Follow, actor: actor, object: sender
  condition "https://www.w3.org/ns/activitystreams#Public", IsRecipient
  none Inbox, owner: actor, activity: activity
  assert Inbox, owner: actor, activity: activity
  assert activity, IsAddressedTo, actor
end

rule "inbox"
  condition Incoming, actor, activity
  condition Activity, activity, actor: sender
  condition Follow, actor: actor, object: sender
  condition sender.followers, IsRecipient
  none Inbox, owner: actor, activity: activity
  assert Inbox, owner: actor, activity: activity
  assert activity, IsAddressedTo, actor
end

# Notifications

rule "create/mention"
  condition activity, IsAddressedTo, actor
  condition CreateActivity, activity, object: object
  any Mention, mention, subject: object, href: actor.iri
  none Notification, owner: actor, activity: activity
  assert Notification, owner: actor, activity: activity
end

rule "create/reply"
  condition activity, IsAddressedTo, actor
  condition CreateActivity, activity, object: object
  condition Object, object, in_reply_to: other
  condition Object, other, attributed_to: actor
  none Notification, owner: actor, activity: activity
  assert Notification, owner: actor, activity: activity
end

rule "announce"
  condition activity, IsAddressedTo, actor
  condition AnnounceActivity, activity, object: object
  condition Object, object, attributed_to: actor
  none Notification, owner: actor, activity: activity
  assert Notification, owner: actor, activity: activity
end

rule "like"
  condition activity, IsAddressedTo, actor
  condition LikeActivity, activity, object: object
  condition Object, object, attributed_to: actor
  none Notification, owner: actor, activity: activity
  assert Notification, owner: actor, activity: activity
end

rule "follow"
  condition activity, IsAddressedTo, actor
  condition FollowActivity, activity, object: actor
  none Notification, owner: actor, activity: activity
  assert Notification, owner: actor, activity: activity
end

rule "delete"
  condition delete, IsAddressedTo, actor
  condition DeleteActivity, delete, object: object
  condition CreateActivity, activity, object: object
  any Notification, owner: actor, activity: activity
  retract Notification, owner: actor, activity: activity
end

rule "undo"
  condition undo, IsAddressedTo, actor
  condition UndoActivity, undo, object: activity
  any Notification, owner: actor, activity: activity
  retract Notification, owner: actor, activity: activity
end

# Timeline

# the first two rules would be one rule if "or" was supported.
# notify if there are either no replies and no mentions, or the
# actor is mentioned.

rule "create"
  condition activity, IsAddressedTo, actor
  condition CreateActivity, activity, object: object
  none Object, object, in_reply_to: any
  none Mention, mention, subject: object
  none Timeline, owner: actor, object: object
  assert Timeline, owner: actor, object: object
end

rule "create"
  condition activity, IsAddressedTo, actor
  condition CreateActivity, activity, object: object
  any Mention, mention, subject: object, href: actor.iri
  none Timeline, owner: actor, object: object
  assert Timeline, owner: actor, object: object
end

rule "announce"
  condition activity, IsAddressedTo, actor
  condition AnnounceActivity, activity, object: object
  none Timeline, owner: actor, object: object
  assert Timeline, owner: actor, object: object
end

rule "delete"
  condition activity, IsAddressedTo, actor
  condition DeleteActivity, activity, object: object
  any Timeline, owner: actor, object: object
  retract Timeline, owner: actor, object: object
end

rule "undo"
  condition undo, IsAddressedTo, actor
  condition UndoActivity, undo, object: activity
  condition AnnounceActivity, activity, object: object
  none CreateActivity, object: object
  none AnnounceActivity, not activity, object: object
  any Timeline, owner: actor, object: object
  retract Timeline, owner: actor, object: object
end
