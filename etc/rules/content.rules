# These are the rule definitions for Ktistec. Rules define how inbound
# and outbound activities are processed and stored in collections.
#
# Rules are processed in order of their appearance in this file. For
# each rule, conditions are tested against facts and all combinations
# of facts for which all conditions were true are placed into an
# agenda. Once matching is complete, all of the actions associated
# with each match are executed. It's not possible for an action to
# change the contents of the agenda -- for each rule, all matches are
# computed and then actions are executed. An action can influence the
# matches for subsequent rules, however.
#
# Rules have the following syntax:
#   rule <name>
#     [optional trace]
#     <condition 1>
#     <condition 2>
#       ...
#     <condition N>
#     <action 1>
#     <action 2>
#       ...
#     <action M>
#   end
#
# <name> is a string of characters, enclosed in quotes. It does not
# have to be unique.
#
# Each <condition n> must begin with "condition", "any", or
# "none". The keyword "condition" specifies a pattern that must match
# a fact for the condition to be true. The keyword "any" specifies a
# pattern that must match at least once fact for the condition to be
# true. The keyword "none" specifies a pattern that must not match any
# facts for the condition to be true.
#
# Each <action m> must begin with "assert" or "retract". The keyword
# "assert" asserts a fact, adding it to the database. The keyword
# "retract" retracts a fact, removing it from the database.
#
# The keyword "trace" is optional. If specified in a rule, it enables
# tracing for the rule. Tracing dumps information about the matching
# process to the console, which can assist with debugging.
#
# Capitalized words (e.g. Object) are names of classes of facts.
# Uncapitalized words (e.g. owner) are variables that can be bound to
# values during matching and then used to constrain subsequent
# conditions. A variable can be bound to a fact, or to a string or
# numeric value.
#
# Some facts have fields, which are specified in patterns by names
# ending in a colon (e.g. owner:). Fields constrain the instances of a
# fact class that match a pattern.
#
# Consider a condition like the following:
#   condition Actor actor, iri: "https://foo.bar/baz"
#
# It will only match instances of the class Actor with the IRI
# "https://foo.bar/baz". Since IRIs happen to be unique, it will match
# one instance or none at all. If successful, it will bind `actor` to
# the matched fact.
#
# Consider a condition like the following where the string literal has
# been replaced by a variable:
#   condition Actor actor, iri: iri
#
# It will match all instances of the class Actor -- failing only if
# there are none. If successful, it will bind `actor` and `iri` to
# each matched fact and its IRI.
#
# Bound variables can be used in later conditions. Consider the
# following pair of conditions:
#   condition Actor actor, iri: "https://foo.bar/baz"
#   condition Object, object, attributed_to: actor
#
# These conditions will only match successfully if there is an
# instance of the class Actor with the IRI "https://foo.bar/baz" and
# at least one instance of the class Object which is attributed to
# that actor. If this were part of a rule, the rule actions would be
# executed for each such instance of class Object. Bound values for
# actor and object would be available to each action invocation.

## Outbox

rule "outbox"
  condition Outgoing, actor, activity
  none Outbox, owner: actor, activity: activity
  assert Outbox, owner: actor, activity: activity
  assert activity, InMailboxOf, actor
end

## Filter Incoming Content

rule "filter content/create"
  condition Incoming, actor, activity
  condition CreateActivity, activity, actor: not actor, object: object
  any Filter, actor: actor, term: filter(strip(object.content))
  retract Incoming, actor, activity
end

rule "filter content/announce"
  condition Incoming, actor, activity
  condition AnnounceActivity, activity, actor: not actor, object: object
  any Filter, actor: actor, term: filter(strip(object.content))
  retract Incoming, actor, activity
end

## Inbox

rule "inbox"
  condition Incoming, actor, activity
  condition Actor, actor, iri: iri
  condition iri, IsRecipient
  none Inbox, owner: actor, activity: activity
  assert Inbox, owner: actor, activity: activity
  assert activity, InMailboxOf, actor
end

rule "inbox"
  condition Incoming, actor, activity
  condition Activity, activity, actor: sender
  condition Follow, actor: actor, object: sender
  condition "https://www.w3.org/ns/activitystreams#Public", IsRecipient
  none Inbox, owner: actor, activity: activity
  assert Inbox, owner: actor, activity: activity
  assert activity, InMailboxOf, actor
end

rule "inbox"
  condition Incoming, actor, activity
  condition Activity, activity, actor: sender
  condition Follow, actor: actor, object: sender
  condition sender.followers, IsRecipient
  none Inbox, owner: actor, activity: activity
  assert Inbox, owner: actor, activity: activity
  assert activity, InMailboxOf, actor
end

## Mailbox Notifications

rule "object activity/reply"
  condition activity, InMailboxOf, actor
  condition ObjectActivity, activity, object: object
  condition Object, object, attributed_to: not actor
  condition Object, object, in_reply_to: other
  condition Object, other, attributed_to: actor
  none NotificationMention, owner: actor, object: object
  none NotificationReply, owner: actor, object: object
  assert NotificationReply, owner: actor, object: object
end

rule "delete/reply"
  condition delete, InMailboxOf, actor
  condition DeleteActivity, delete, object: object
  any NotificationReply, owner: actor, object: object
  retract NotificationReply, owner: actor, object: object
end

rule "object activity/mention"
  condition activity, InMailboxOf, actor
  condition ObjectActivity, activity, object: object
  condition Object, object, attributed_to: not actor
  any Mention, mention, subject: object, href: actor.iri
  none NotificationMention, owner: actor, object: object
  none NotificationReply, owner: actor, object: object
  assert NotificationMention, owner: actor, object: object
end

rule "delete/mention"
  condition delete, InMailboxOf, actor
  condition DeleteActivity, delete, object: object
  any NotificationMention, owner: actor, object: object
  retract NotificationMention, owner: actor, object: object
end

rule "announce, step 1"
  condition activity, InMailboxOf, actor
  condition AnnounceActivity, activity, object: object
  condition Object, object, attributed_to: actor
  # remove any previous notifications about this object
  condition AnnounceActivity, previous, object: object
  condition NotificationAnnounce, owner: actor, activity: previous
  retract NotificationAnnounce, owner: actor, activity: previous
end

rule "announce, step 2"
  condition activity, InMailboxOf, actor
  condition AnnounceActivity, activity, object: object
  condition Object, object, attributed_to: actor
  none NotificationAnnounce, owner: actor, activity: activity
  assert NotificationAnnounce, owner: actor, activity: activity
end

rule "undo/announce"
  condition undo, InMailboxOf, actor
  condition UndoActivity, undo, object: activity
  any NotificationAnnounce, owner: actor, activity: activity
  retract NotificationAnnounce, owner: actor, activity: activity
end

rule "like, step 1"
  condition activity, InMailboxOf, actor
  condition LikeActivity, activity, object: object
  condition Object, object, attributed_to: actor
  # remove any previous notifications about this object
  condition LikeActivity, previous, object: object
  condition NotificationLike, owner: actor, activity: previous
  retract NotificationLike, owner: actor, activity: previous
end

rule "like, step 2"
  condition activity, InMailboxOf, actor
  condition LikeActivity, activity, object: object
  condition Object, object, attributed_to: actor
  none NotificationLike, owner: actor, activity: activity
  assert NotificationLike, owner: actor, activity: activity
end

rule "undo/like"
  condition undo, InMailboxOf, actor
  condition UndoActivity, undo, object: activity
  any NotificationLike, owner: actor, activity: activity
  retract NotificationLike, owner: actor, activity: activity
end

rule "dislike, step 1"
  condition activity, InMailboxOf, actor
  condition DislikeActivity, activity, object: object
  condition Object, object, attributed_to: actor
  # remove any previous notifications about this object
  condition DislikeActivity, previous, object: object
  condition NotificationDislike, owner: actor, activity: previous
  retract NotificationDislike, owner: actor, activity: previous
end

rule "dislike, step 2"
  condition activity, InMailboxOf, actor
  condition DislikeActivity, activity, object: object
  condition Object, object, attributed_to: actor
  none NotificationDislike, owner: actor, activity: activity
  assert NotificationDislike, owner: actor, activity: activity
end

rule "undo/dislike"
  condition undo, InMailboxOf, actor
  condition UndoActivity, undo, object: activity
  any NotificationDislike, owner: actor, activity: activity
  retract NotificationDislike, owner: actor, activity: activity
end

rule "follow"
  condition activity, InMailboxOf, actor
  condition FollowActivity, activity, object: actor
  none NotificationFollow, owner: actor, activity: activity
  assert NotificationFollow, owner: actor, activity: activity
end

rule "undo/follow"
  condition undo, InMailboxOf, actor
  condition UndoActivity, undo, object: activity
  any NotificationFollow, owner: actor, activity: activity
  retract NotificationFollow, owner: actor, activity: activity
end

## Follow Notifications

# only notify on the *first* activity for an object!

rule "incoming activity for object"
  condition Incoming, actor, activity
  condition ObjectActivity, activity, object: object
  none ObjectActivity, not activity, object: object
  assert CheckFollowFor, actor, object
end

rule "notification exists for object 1"
  condition CheckFollowFor, actor, object
  condition Object, object, attributed_to: not actor
  condition Hashtag, subject: object, name: name
  condition NotificationFollowHashtag, owner: actor, name: name
  retract NotificationFollowHashtag, owner: actor, name: name
end

rule "notification exists for object 2"
  condition CheckFollowFor, actor, object
  condition Object, object, attributed_to: not actor
  condition Mention, subject: object, name: name
  condition NotificationFollowMention, owner: actor, name: name
  retract NotificationFollowMention, owner: actor, name: name
end

rule "notification exists for object 3"
  condition CheckFollowFor, actor, object
  condition Object, object, thread: thread
  condition Object, root, iri: thread
  condition NotificationFollowThread, owner: actor, object: root
  retract NotificationFollowThread, owner: actor, object: root
end

rule "follow/hashtag"
  condition CheckFollowFor, actor, object
  condition Object, object, attributed_to: not actor
  condition Hashtag, subject: object, name: name
  any FollowHashtag, actor: actor, name: name
  assert NotificationFollowHashtag, owner: actor, name: name
end

rule "follow/mention"
  condition CheckFollowFor, actor, object
  condition Object, object, attributed_to: not actor
  condition Mention, subject: object, name: name
  any FollowMention, actor: actor, name: name
  assert NotificationFollowMention, owner: actor, name: name
end

rule "follow/thread"
  condition CheckFollowFor, actor, object
  condition Object, object, thread: thread
  condition FollowThread, actor: actor, thread: thread
  condition Object, root, iri: thread
  none NotificationFollowThread, owner: actor, object: root
  assert NotificationFollowThread, owner: actor, object: root
end

## Timeline

# the first two rules would be one rule if "or" was supported.
# notify if there are either no replies and no mentions, or the
# actor is mentioned.

rule "create/none"
  condition activity, InMailboxOf, actor
  condition CreateActivity, activity, object: object
  none Object, object, in_reply_to: any
  none Mention, mention, subject: object
  none Timeline, owner: actor, object: object
  assert TimelineCreate, owner: actor, object: object
end

rule "create/owner"
  condition activity, InMailboxOf, actor
  condition CreateActivity, activity, object: object
  condition Object, object, attributed_to: actor
  none Timeline, owner: actor, object: object
  assert TimelineCreate, owner: actor, object: object
end

rule "create/mention"
  condition activity, InMailboxOf, actor
  condition CreateActivity, activity, object: object
  any Mention, mention, subject: object, href: actor.iri
  none Timeline, owner: actor, object: object
  assert TimelineCreate, owner: actor, object: object
end

rule "announce"
  condition activity, InMailboxOf, actor
  condition AnnounceActivity, activity, object: object
  none Timeline, owner: actor, object: object
  assert TimelineAnnounce, owner: actor, object: object
end

rule "delete"
  condition activity, InMailboxOf, actor
  condition DeleteActivity, activity, object: object
  any Timeline, owner: actor, object: object
  retract Timeline, owner: actor, object: object
end

rule "undo"
  condition undo, InMailboxOf, actor
  condition UndoActivity, undo, object: activity
  condition AnnounceActivity, activity, object: object
  none CreateActivity, object: object
  none AnnounceActivity, not activity, object: object
  any Timeline, owner: actor, object: object
  retract Timeline, owner: actor, object: object
end
